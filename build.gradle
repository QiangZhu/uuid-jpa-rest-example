buildscript {
    ext { springBootVersion = '1.4.0.M1' }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
    }
    dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'demo'
    version = '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
}

configurations { instrument { description 'classpath for instrument as java agent' } }

dependencies {
    instrument('org.springframework:spring-instrument')

    compile("org.springframework.boot:spring-boot-starter-data-jpa") { exclude module: 'hibernate-entitymanager' }
    compile 'org.eclipse.persistence:org.eclipse.persistence.jpa:2.6.1'
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile('com.fasterxml.uuid:java-uuid-generator:3.1.3')

    runtime('org.springframework.data:spring-data-rest-hal-browser')
    runtime("com.h2database:h2")
    runtime 'mysql:mysql-connector-java'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testRuntime('com.jayway.jsonpath:json-path:2.2.0')

    // gradle 2.12 feature
    compileOnly('org.projectlombok:lombok:1.16.8')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:1.4.0.M1"
    }
}

test { jvmArgs "-javaagent:$project.configurations.instrument.singleFile" }

bootRun {
    jvmArgs "-javaagent:$project.configurations.instrument.singleFile"
    systemProperties = System.properties
}

task copySpringInstrumentJar (type:Copy) {
    description = 'Copies the spring-instrument.jar into your lib directory.'
    outputs.file project.file('lib/spring-instrument.jar')
    from (project.configurations.instrument)
    into("lib")
    rename { String filename -> filename = 'spring-instrument.jar' }
}

tasks.idea.dependsOn(project.tasks.copySpringInstrumentJar)
tasks.eclipse.dependsOn(project.tasks.copySpringInstrumentJar)
tasks.processResources.dependsOn(project.tasks.copySpringInstrumentJar)
