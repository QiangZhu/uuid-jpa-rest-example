buildscript {
    ext { springBootVersion = '1.4.0.RELEASE' }
    repositories {
        mavenCentral()
    }
    dependencies { classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")  }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

jar {
    baseName = 'demo'
    version = '0.0.1-SNAPSHOT'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

task dependencyManagementProperties << {
    description = 'Print all BOM properties to the console'
 
    // Sort properties and print to console.
    dependencyManagement
        .importedProperties
        .sort()
        .each { property -> println property }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

configurations {
    instrument { 
        description 'classpath for instrument as java agent' 
    }
}

dependencies {
    // Tooling
    compileOnly 'org.projectlombok:lombok:1.16.8'
    instrument('org.springframework:spring-instrument')

    // Database
    compile("org.springframework.boot:spring-boot-starter-data-jpa") { exclude module: 'hibernate-entitymanager' }
    compile 'org.eclipse.persistence:org.eclipse.persistence.jpa:2.6.2'
    runtime("com.h2database:h2")
    runtime 'mysql:mysql-connector-java'

    // REST
    runtime('org.springframework.data:spring-data-rest-hal-browser')
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    runtime 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // Util
    compile 'com.google.guava:guava:19.0'
    compile('com.fasterxml.uuid:java-uuid-generator:3.1.3')

    // Testing
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:1.4.0.RELEASE"
    }
}

test { jvmArgs "-javaagent:$project.configurations.instrument.singleFile" }

bootRun {
    jvmArgs "-javaagent:$project.configurations.instrument.singleFile"
    systemProperties = System.properties
}

task copySpringInstrumentJar (type:Copy) {
    description = 'Copies the spring-instrument.jar into your lib directory.'
    outputs.file project.file('lib/spring-instrument.jar')
    from (project.configurations.instrument)
    into("lib")
    rename { String filename -> filename = 'spring-instrument.jar' }
}

tasks.idea.dependsOn(project.tasks.copySpringInstrumentJar)
tasks.eclipse.dependsOn(project.tasks.copySpringInstrumentJar)
tasks.processResources.dependsOn(project.tasks.copySpringInstrumentJar)
